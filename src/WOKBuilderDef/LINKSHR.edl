-- File:	LINKSHR.edl
-- Author:	Jean GAUTIER
-- History:	Fri Feb 16 17:04:32 1996	Jean GAUTIER	Creation
-- Copyright:	 Matra Datavision 1996

@ifnotdefined ( %LINKSHR_EDL ) then
@set %LINKSHR_EDL = "";

@ifnotdefined ( %LDSHR_EDL ) then
  @uses "LDSHR.edl";
@endif;

@set %LINKSHR_Header          = "LINKSHR_Header";
@set %LINKSHR_ObjectRef       = "LD_ObjectRef";
@set %LINKSHR_ShortRef        = "LD_ShortRef";
@set %LINKSHR_LongRef         = "LD_LongRef";
@set %LINKSHR_SharedFullPath  = "LD_SharedFullPath";
@set %LINKSHR_ArchiveFullPath = "LDSHR_ArchiveFullPath";
@set %LINKSHR_ExternRef       = "LD_ExternRef";
@set %LINKSHR_Footer          = "LD_Footer";

@if ( %Station == "sun" ) then
  @set %LINKSHR_DBMSOpt = "-mt";
@else
  @set %LINKSHR_DBMSOpt = "";
@endif;

@if ( %Station != "hp" ) then
  @template LINKSHR_FileName ( %LINKSHR_LibName ) is
   $lib%LINKSHR_LibName.so\^
  @end;
@endif;

@if ( %Station == "hp" ) then
  @template LINKSHR_FileName ( %LINKSHR_LibName ) is
   $lib%LINKSHR_LibName.sl\^
  @end;
@endif;


@template LINKSHR_ArchiveFullPath ( %LibDir, %LibName ) is
$ -all %LibDir/lib%LibName.a -none \
@end;

@if( %Station == "sun" ) then 

  @set %LINKSHR_Footer          = "LINKSHR_Footer";

  @set %LINKSHR_ArchiveFullPath = "LINKSHR_ArchiveFullPath";

  @template LINKSHR_ArchiveFullPath ( ) is
   $\^
  @end;

  @template LINKSHR_Header ( %Target, %LINKSHR_DBMSOpt ) is 
   $/opt/DEV5_1/SUNWspro/bin/CC -xchip=ultra -xarch=v8  -G %LINKSHR_DBMSOpt -o %Target \
  @end;

  @template LINKSHR_Footer ( ) is
    $ -lC -lthread -lc ;
  @end;

@endif;

@if( %Station == "ao1") then

  @set %LINKSHR_LibLimit = "300";

  @template LINKSHR_Header ( %LINKSHR_DBMSOpt, %Target, %LogicalName ) is 
   $cxx -shared %LINKSHR_DBMSOpt -msym -o %Target -soname %LogicalName \
  @end;

@endif;

@if( %Station == "sil" ) then

  @set %LINKSHR_ObjectRef       = "LINKSHR_ObjectRef";

  @template LINKSHR_ObjectRef ( %ObjectPath ) is
   $\^
  @end;

  @template LINKSHR_Header ( %LINKSHR_DBMSOpt, %Target, %LogicalName, %LD_ObjList ) is
   $/usr/bin/CC -n32 -mips3 -Wl,-mmap -multigot -no_prelink -shared %LINKSHR_DBMSOpt -o %Target -soname %LogicalName \
   $  -all -objectlist %LD_ObjList -none \
  @end;

@endif;

@if( %Station == "hp" ) then

  @set %LINKSHR_Footer          = "LINKSHR_Footer";
  @set %LINKSHR_ObjectRef       = "LINKSHR_ObjectRef";

  @template LINKSHR_Header ( %LINKSHR_DBMSOpt, %Target, %LD_ObjList, %LogicalName ) is
    $\mv %LD_ObjList %LD_ObjList.o
    $/opt/aCC/bin/aCC %LINKSHR_DBMSOpt -b -o %Target  \
    $-Wl,-c%LD_ObjList.o \
  @end;

  @template LINKSHR_Footer (%LD_ObjList) is
    $;
    $set lnk_status = $status
    $\mv %LD_ObjList.o %LD_ObjList;
    $set status = $lnk_status
  @end;

  @template LINKSHR_ObjectRef () is
    $\^
  @end;

@endif;

@if ( %Station == "lin" ) then
 @set %LINKSHR_Footer          = "LINKSHR_Footer";
 @set %LINKSHR_ArchiveFullPath = "LINKSHR_ArchiveFullPath";

 @template LINKSHR_ArchiveFullPath ( ) is
  $\^
 @end;

 @template LINKSHR_Header ( %Target, %LINKSHR_DBMSOpt ) is
  $/usr/bin/g++ -shared %LINKSHR_DBMSOpt -o %Target \
 @end;

 @template LINKSHR_Footer ( ) is
  $ ;
 @end;

 @template LINKSHR_CheckUndefHeader ( %Target, %LINKSHR_DBMSOpt ) is
  $/usr/bin/g++ -nostartfiles %LINKSHR_DBMSOpt -o /dev/null \
 @end;

 @template LINKSHR_CheckUndefFooter ( ) is
  $ ;
 @end;

@endif;
@endif;

