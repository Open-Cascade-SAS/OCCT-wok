#!/bin/csh -f
#
# This file is a small easy wok launcher and configurator. 
# It will be used as a template for configuring your wok
# Important restriction: 
# The environment setted below allows you to use Wok as a tool
# for compiling and linking.
# It CANNOT be used to execute and/or set adequate environment for your
# applications to work.
# Different usages:
#
# 1. Launches a Tcl script file. 
#    > wokcfg script myscript.tcl
# 2. Creates a c-shell proc to launch wok according to a previous configuration
#    > wokcfg configure dir1 dir2 myproc
# 3. Creates a minimal set of wok entities to begin work under wok.
#    > wokcfg setup home_entities myFac myShop MyWb 
# 4. Import a directory tree containing a previous Cascade download. Such a tree
#    looks like /someroot/src, /someroot/adm, /someroot/inc, /someroot/drv
#    This will create workbench MyWb and populate it with a Cascade download found in /someroot.
#    > wokcfg import home_entities myFac myShop MyWb  /someroot
#
set noglob ; onintr bye ; set narg = $#argv ; set here = `pwd`

set justrun=1
set script=0
switch  ( $narg )

  case '2': 
    if ( "$argv[1]" == "script" ) then
	if (-e $argv[2]) then
	    set justrun=1
	    set script=1
	    set scriptfile=$argv[2]
	else	
	    echo " the Script File : $argv[2] does not exist "
	endif    
    endif	
    breaksw

  case '3' :  
    if ( "$argv[1]" == "configure" ) then
       set fact=$argv[2]
       set temp=$argv[3]
       if ( ! -d $fact) then
          echo "Creating directory $fact."
          mkdir $fact
       endif
       cp -f $here/WOKSESSION.edl ${fact}/.
       cp -f $here/DEFAULT.edl    ${fact}/.
       cp -f $here/tclshrc_Wok    ${fact}/.
       set root=`dirname $here`
       sed -e s@TOSUBSTITUTE_WOKHOME@$root@g -e s@TOSUBSTITUTE_FACTORYPROC@$fact@g $0 >$temp
       chmod +x $temp
       echo "File $temp has been created."
       exit
  endif
  breaksw

  case '5' :
  if ( "$argv[1]" == "setup" ) then
    set justrun=0
    set home_entities=$argv[2]
    set factoryname=$argv[3] 
    set workshopname=$argv[4]
    set workbenchname=$argv[5]
    set importdir=/dev/null
  endif
  breaksw

  case '6' :
  if ( "$argv[1]" == "import" ) then
    set justrun=0
    set home_entities=$argv[2] 
    set factoryname=$argv[3] 
    set workshopname=$argv[4]
    set workbenchname=$argv[5]
    set importdir=$argv[6] 
  endif
  breaksw


endsw

setenv WOKHOME         TOSUBSTITUTE_WOKHOME
setenv WOK_ROOTADMDIR  TOSUBSTITUTE_FACTORYPROC
setenv WOK_SESSIONID   $HOME/.wok

if (! ($?LD_LIBRARY_PATH) ) setenv LD_LIBRARY_PATH ""

setenv WOK_LIBRARY     $WOKHOME/lib 

switch ( `uname` )
    
    case IRIX*:
        setenv WOKSTATION sil
        setenv TCLHOME         /usr/tcltk
        breaksw

    case SunOS:
	setenv WOKSTATION sun 
	setenv TCLHOME         /usr/tcltk 
        breaksw
    
    case AIX:
	setenv WOKSTATION aix
	setenv TCLHOME         /usr/local
	breaksw
    
    case Linux:
	setenv WOKSTATION lin
        setenv TCLHOME         /usr 
        breaksw                                
    
    case HP-UX:
	setenv WOKSTATION hp
        setenv TCLHOME         /usr/tcltk  
        breaksw  
    
    default:
	echo "Error : This platform is not supported."
	exit
endsw

setenv PATH            "${TCLHOME}/bin:${PATH}"
setenv WOK_LIBPATH     ${WOK_LIBRARY}:${WOK_LIBRARY}/${WOKSTATION}
setenv LD_LIBRARY_PATH ${LD_LIBRARY_PATH}:${WOK_LIBRARY}/${WOKSTATION}
if ( "`uname`" == "HP-UX" ) setenv SHLIB_PATH "${LD_LIBRARY_PATH}:${TCLHOME}/lib/"
if ( "`uname`" == "AIX" ) setenv LIBPATH "${LD_LIBRARY_PATH}:${TCLHOME}/lib/"

if ( $justrun ) then
   echo "Now running tclsh using :"
   echo "LD_LIBRARY_PATH = $LD_LIBRARY_PATH"
   echo "WOKHOME         = $WOKHOME"
   echo "PATH            = $PATH"
   if ( $script ) then
      ${TCLHOME}/bin/tclsh  $scriptfile
   else
      ${TCLHOME}/bin/tclsh
   endif   
   echo "Bye bye `whoami`"
else
    sed -e s@TOSUBSTITUTE@$WOK_ROOTADMDIR@g  $WOKHOME/site/WOKSESSION.edl > ${WOK_ROOTADMDIR}/WOKSESSION.edl 
    sed -e s@/dp_xx@$home_entities@g        $WOKHOME/site/DEFAULT.edl    > ${WOK_ROOTADMDIR}/DEFAULT.edl
   ${TCLHOME}/bin/tclsh $WOKHOME/site/CreateFactory.tcl \
    $home_entities $factoryname $workshopname $workbenchname $importdir
endif

bye:
exit


